plugins {
    // Required for NeoGradle
    //id 'idea'
    //id 'java'
    //id "org.jetbrains.gradle.plugin.idea-ext" version "1.1.7"
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.4-SNAPSHOT" apply false
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    java.toolchain.languageVersion = JavaLanguageVersion.of(17)
    java.withSourcesJar()
    //java.withJavadocJar()

    loom {
        silentMojangMappingsLicense()
    }

    jar {
        from(rootProject.file("LICENSE"))
        from(rootProject.file("CREDITS.md"))
    }

    processResources {
        def expandProps = [
                "version": mod_version,
                "group": project.group, //Else we target the task's group.
                "minecraft_version": minecraft_version,
                "forge_version": forge_version,
                "forge_loader_version_range": forge_loader_version_range,
                "forge_version_range": forge_version_range,
                "minecraft_version_range": minecraft_version_range,
                "fabric_version": fabric_version,
                "fabric_loader_version": fabric_loader_version,
                "clothapi_version": clothapi_version,
                "yacl_version": yacl_version,
                "mod_name": mod_name,
                "mod_author": mod_author,
                "mod_id": mod_id,
                "mod_homepage": mod_homepage,
                "mod_sources": mod_sources,
                "mod_issues": mod_issues,
                "curseforge_slug": curseforge_slug,
                "modrinth_slug": modrinth_slug,
                "license": license,
                "description": project.description,
                "neoforge_version": neoforge_version,
                "neoforge_loader_version_range": neoforge_loader_version_range,
                "credits": credits
        ]

        filesMatching(['pack.mcmeta', 'fabric.mod.json', 'META-INF/mods.toml', '*.mixins.json']) {
            expand expandProps
        }
        inputs.properties(expandProps)
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        // The following line declares the mojmap mappings, you may use other mappings as well
        mappings loom.officialMojangMappings()
        // The following line declares the yarn mappings you may select this one as well.
        // mappings "net.fabricmc:yarn:1.20.1+build.10:v2"
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    base {
        archivesName = rootProject.mod_name
    }

    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories{
        maven { url "https://maven.shedaniel.me/" }
        maven { url "https://maven.terraformersmc.com/" }
        maven { url 'https://maven.isxander.dev/releases' }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}
